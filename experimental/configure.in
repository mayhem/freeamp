dnl $Id: configure.in,v 1.10 1999/04/28 21:36:12 dogcow Exp $
dnl FreeAmp - The Free MP3 Player
dnl Portions copyright (C) 1998 GoodNoise
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
dnl

dnl Process this file with your mom to produce a configure script
AC_INIT(base/src/player.cpp)
AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_HOST

AC_DEFINE_UNQUOTED(FREEAMP_VERSION, "1.2.3")

AC_PROG_MAKE_SET

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_ISC_POSIX
AC_PROG_RANLIB
AC_PROG_INSTALL
AC_CHECK_PROGS(PERL, perl)
AC_CHECK_PROGS(NASM, nasm)

dnl Checks for libraries.

dnl Checks for header files.
AC_STDC_HEADERS
AC_HAVE_HEADERS(unistd.h io.h pthread.h errno.h)

x11="true"

AC_ARG_ENABLE(x11,
	[  --enable-x11            Build the FreeAmp X11 GUI],
	[case "${enableval}" in
	  yes) x11="true" ;;
	  no) x11="false" ;;
	  *) AC_MSG_ERROR(bad value ${enableval} for --enable-x11) ;;
	esac])

dnl ******************
dnl Checks for X stuff
dnl ******************

if test "$x11"="true"; then
  AC_PATH_X
  AC_PATH_XTRA

  if test "x$no_x" = "xyes"; then 
     x11="false"
     echo X11 not found. X11 UI build disabled.
  else

    CFLAGS="$CFLAGS $X_CFLAGS"
    LDFLAGS="$LDFLAGS $X_LDFLAGS $X_LIBS"

    AC_CHECK_LIB(X11, XOpenDisplay, x11="true",
      x11="false"
      echo X11 not found. X11 UI build disabled.
    )

    if test "$x11" = "true"; then
       AC_CHECK_LIB(Xext, XShapeQueryExtension, x11="true",
         x11="false"
         echo X11 Shape Extension not found.  X11 UI build disabled.
       )

    fi

    if test "$x11" = "true"; then
       AC_CHECK_HEADER(X11/xpm.h, x11="true",
         x11="false"
         echo XPM Header not found. X11 UI build disabled.
       )
    fi
  fi
  dnl ******************
  dnl Check for libc5.  If found warn the user about potential problems
  dnl ******************

  AC_MSG_CHECKING(for libc5)
  AC_TRY_RUN([
#include <stdio.h>
#include <ctype.h>
asm (".weak gnu_get_libc_version");
asm (".weak __libc_version");
asm (".weak __linux_C_lib_version");

extern const char * gnu_get_libc_version (void);
extern const char * __linux_C_lib_version;
extern const char __libc_version [];

int main() {
  int libcmajor = 0, libcminor = 0, libcteeny = 0;
  if (((&__linux_C_lib_version != 0)
       && ((&__libc_version != 0) || (gnu_get_libc_version != 0)))
      || (!(&__linux_C_lib_version != 0) && !(&__libc_version != 0)
         && !(gnu_get_libc_version != 0)))
  {
    libcmajor = 0;
    libcminor = 0;
    libcteeny = 0;
  }
  else
  {
    const char * ptr;
    int glibcmajor = 0;

    if (gnu_get_libc_version != 0)
    {
      ptr = gnu_get_libc_version ();
      glibcmajor = 4;
    }
    else if (&__libc_version != 0)
    {
      ptr = __libc_version;
      glibcmajor = 4;
    }
    else
      ptr = __linux_C_lib_version;

    while (!isdigit (*ptr))
      ptr++;

    sscanf (ptr, "%d.%d.%d", &libcmajor, &libcminor, &libcteeny);
    libcmajor += glibcmajor;
  }
  if ((libcmajor == 5) && (!(libcminor == 99))) // see xc/config/cf/linux.cf in the XFree86 distribution
    return 0;
  else
    return 1;
}
  
    ],
    AC_DEFINE(HAVE_LINUX_LIBC5)
    AC_MSG_RESULT(yes)
echo "***"
echo "*** X11 compilation has been enabled and libc5 was found."
echo "*** This means it is likely that your libX11 was built with"
echo "*** libc5 and the X11 defaults for libc5 on linux."
echo "***"
echo "*** There may be unexplained XIO errors causing FreeAmp not to"
echo "*** work.  See http://www.freeamp.org/x11.html for details."
echo "***"
    ,
    AC_MSG_RESULT(no),
    AC_MSG_RESULT(assuming no)
  )



fi

dnl ******************
dnl Check for dynamic loading library
dnl ******************
AC_CHECK_LIB(dl, dlopen, foo="bar",
  AC_MSG_ERROR([FreeAmp requires the ability to load libraries dynamically (-ldl must work)])
)

dnl Check for some more stuff

cmdline="true"
isunix="true"
OSLINK=""
FPIC="-fpic"
LINKMOD="gcc -shared"

case "$host_os" in
  windowsnt)
    cmdline="false"
    isunix="false"
    ;;
  win32)
    isunix="false"
    ;;
  beos*)
    isunix="false"
    ;;
  linux*)
    AC_DEFINE(HAVE_LINUX)
dnl  OSLINK='-Wl,soname,${*F}'
    LDFLAGS="$LDFLAGS -Wl,--export-dynamic"
dnl not needed.
    ;;
  solaris*)
    host_os="solaris"
    if test "$GCC" != "yes" ; then
      dnl probably Sun CC.
      FPIC="-Kpic"
      LINKMOD="$CC -G"	
    fi
    dnl thank you, oh autoconf, for the oh-so-accurately named
    dnl AC_EGREP_HEADER, which only uses egrep after it PUTS THE
    dnl THING THROUGH CPP, MAKING #defineS NOT SHOW UP!
    dnl This is why we use AC_EGREP_CPP instead.
    AC_EGREP_CPP(yes,
[#include <pthread.h>
#ifdef PTHREAD_MUTEX_ERRORCHECK
   yes
#endif
],
      CFLAGS="$CFLAGS -DSOLDEBUGMUTEX"
        AC_MSG_RESULT("Using Solaris debugging mutexen"),
      AC_MSG_RESULT("Not using Solaris debugging mutexen")
    )
    ;;
  *)
    echo "what are you?"
    ;;
    
esac  

AC_DEFINE_UNQUOTED(HOST_OS, "${host_os}")
AC_DEFINE_UNQUOTED(HOST_CPU, "${host_cpu}")


dnl the following is to so that the right includes (and only the right
dnl includes) are added in at compile time.
OSINC="-Ibase/unix/include -Ibase/unix/$host_os/include -Iui/freeamp/unix/include -Iui/freeamp/unix/res"
OSDEP="unix/$host_os"

if test "$isunix" = "false"  ; then
  OSINC="-Ibase/$host_os/include -Iui/freeamp/$host_os"
  OSDEP="$host_os"
else
  OSDEPSRC=`find base/unix/src \( -name '*.c' -o -name '*.cpp' \) -print`
fi

OSDEPSRC="$OSDEPSRC `find base/$OSDEP/src \( -name '*.c' -o		\
					     -name '*.cpp' \) -print`"
OSDEPOBJ=`echo $OSDEPSRC | sed -e 's,\.cp*,.o,g' | xargs echo`
dnl I know. This is an abominable way to generate a dependency list.
dnl it's either here or hardcoded in a Makefile, though.

dnl AC_SUBST(OSDEPSRC)
AC_SUBST(OSDEPOBJ)
AC_SUBST(OSDEP)
AC_SUBST(OSINC)
AC_SUBST(OSLINK)
AC_SUBST(FPIC)
AC_SUBST(LINKMOD)
AC_SUBST(build_cpu)
AC_SUBST(CFLAGS)

gtk="false"

AC_ARG_ENABLE(gtk,
	      [  --enable-gtk            Build GTK+ version of FreeAmp],
              [case "${enableval}" in
		yes) gtk="true" ;;
		no)  gtk="false" ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-gtk) ;;
	      esac])

#if test "$gtk" = "true"; then
dnl ****
dnl Check for GTK+
dnl ****
#AM_PATH_GTK(1.0.6	, echo "             GTK+ compilation enabled", 
dnl                   AC_MSG_ERROR(GTK+ distribution not found.)
#gtk="false"
#echo GTK+ compilation disabled
#)
#fi

#AM_CONDITIONAL(AM_ENABLE_GTK, test "$gtk" = "true")


mp3prof="false"

AC_ARG_ENABLE(mp3prof,
	[  --enable-mp3prof        Enable profiling of the MP3 decoder (linux only!)],
	[case "${enableval}" in
	  yes) mp3prof="true" ;;
	  no) mp3prof="false" ;;
	  *) AC_MSG_ERROR(bad value ${enableval} for --enable-mp3prof) ;;
	esac])

if test "$mp3prof" = "true"; then
   AC_DEFINE(MP3_PROF)
fi


dnl *****
dnl Check for LinuxThreads or MIT pthreads
dnl *****

mitpth="false"
ltr="false"

if test "$host_os" = "solaris"; then
  AC_MSG_RESULT([This is solaris; using solaris threads.])
elif test "$host_os" = "beos"; then
  AC_MSG_RESULT([This is BeOS; using BeOS threads.])
else

  AC_MSG_CHECKING(for MIT PThreads)
  AC_EGREP_HEADER(PTHREAD_MUTEXTYPE_FAST,
  pthread.h,AC_DEFINE(HAVE_MITPTHREADS) AC_MSG_RESULT(yes)
  mitpth="true", 
  AC_MSG_RESULT(no)
  )
  
  AC_MSG_CHECKING(for Base LinuxThreads)
  AC_EGREP_HEADER(PTHREAD_MUTEX_FAST_NP,pthread.h, AC_DEFINE(HAVE_BASELINUXTHREADS) 
  hblt="true"
  ltr="true"
  AC_MSG_RESULT(yes), 
  AC_MSG_RESULT(no)
  )
  
  AC_MSG_CHECKING(for LinuxThreads w/ErrorCheck Mutex)
  AC_EGREP_HEADER(PTHREAD_MUTEX_ERRORCHECK_NP,pthread.h,AC_DEFINE(HAVE_ENHLINUXTHREADS)
  ltr="true"
  AC_MSG_RESULT(yes), 
  AC_MSG_RESULT(no)
  )
  
  if test "$mitpth" = "false"; then
      if test "$ltr" = "false"; then
  	AC_MSG_CHECKING(for broken Slackware MIT PThreads header (Obj C stub) and implementation)
  	AC_TRY_RUN([
  	    #define _MIT_POSIX_THREADS 1
  	    #include <pthread.h>
  	    int main(int argc,char**argv) {
  		int foo;
                  foo = 5 + PTHREAD_MUTEXTYPE_DEBUG;
  		return 0;
              }
  	    ],
  	    AC_DEFINE(HAVE_BROKENMITPTHREADS)
  	    AC_DEFINE(HAVE_MITPTHREADS)
  	    mitpth="true"
  	    AC_MSG_RESULT(yes),
  	    AC_MSG_RESULT(no),
  	    AC_MSG_RESULT(assuming no)
  	)
      fi
  fi
  
  
  if test "$mitpth" = "false"; then
     if test "$ltr" = "false"; then
        echo "Can't find MIT PThreads or LinuxThreads complete development headers in /usr/include or /usr/local/include"
        echo "Configure will stop."
        exit 1
     fi 
  fi

fi dnl solaris, linux, or what have you. I should really indent those.

dnl *****

dnl Replace `main' with a function in -lasound:
dnl AC_CHECK_LIB(asound, main)

#AM_CONDITIONAL(AM_HAVE_LINUXTHREADS, test "$hblt" = "true")

AC_ARG_ENABLE(cmdline,
	      [  --enable-cmdline        Build command line freeamp],
	      [case "${enableval}" in
	        yes) cmdline="true" ;;
		no)  cmdline="false" ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-cmdline) ;;
	      esac])
#AM_CONDITIONAL(AM_ENABLE_CMDLINE, test "$cmdline" = "true")


AC_ARG_ENABLE(x11,
		[  --enable-x11            Build the FreeAmp X11 GUI],
		[case "${enableval}" in
		  yes) x11="true" ;;
		  no) x11="false" ;;
		  *) AC_MSG_ERROR(bad value ${enableval} for --enable-x11) ;;
		esac])
#AM_CONDITIONAL(AM_ENABLE_X11, test "$x11" = "true")

FAUIPLUGIN=plugins/freeamp-${host_os}.ui
if test "$isunix" = "false" -o "x11 = false"; then
  OSDEPPLUGINS="$OSDEPPLUGINS $FAUIPLUGIN";
else
  AC_MSG_WARN([not compiling freeamp-$host_os ui plugin])
fi

dnl ****
dnl Check for alsa
dnl ****
AC_CHECK_HEADER(sys/asoundlib.h,
		AC_CHECK_LIB(asound, main,
			     have_alsa="true"
			     ,
			     have_alsa="false"
		)
		,
		have_alsa="false"
)

ALSAPMOPLUGIN=plugins/almasoundcard-${host_os}.pmo
if test "$isunix" = "false" -a "x11 = true"; then
  OSDEPPLUGINS="$OSDEPPLUGINS $ALSAPMOPLUGIN";
  OSINC="$OSINC -Iio/soundcard/unix/linux/include"
  AC_MSG_RESULT([compiling alsa soundcard plugin])
fi

AC_SUBST(OSDEPPLUGINS)

if expr "$host_cpu" : "i.86$" > /dev/null; then
  if test -n "$NASM"; then
    AC_MSG_RESULT([Using x86 optimizations])
    XINGASM="`echo lmc/xingmp3/src/{cdct,cwin,cwin8,mdct,msis}asm.o`"
    AC_DEFINE(ASM_X86)
  else
    AC_MSG_RESULT([nasm not available, using older x86 optimizations])
    XINGASM=lmc/xingmp3/src/x86gas.o
    AC_DEFINE(ASM_X86_OLD)
  fi
fi

AC_SUBST(XINGASM)

AC_CONFIG_HEADER(config/config.h)
AC_OUTPUT(Makefile)

